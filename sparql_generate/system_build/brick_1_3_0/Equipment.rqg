prefix nest: <https://www.empa.ch/digital-twin/>
prefix nest_location: <https://www.empa.ch/digital-twin/location/>
prefix nest_equipment: <https://www.empa.ch/digital-twin/equipment/>
prefix nest_point: <https://www.empa.ch/digital-twin/point/>
prefix nest_collection: <https://www.empa.ch/digital-twin/collection/>
prefix brick: <https://brickschema.org/schema/Brick#>
prefix ite: <http://w3id.org/sparql-generate/iter/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

GENERATE {

?NEST_Equipment_URI a ?BrickClass_Type;
    brick:hasLocation ?Location_URI;
    brick:feeds ?feeds_Equipment_URI;
    brick:meters ?metered_unit_URI;
    brick:isSubmeterOf ?isSubmeterOf_Equipment_URI;
    brick:isPartOf ?isPartOf_Equipment_URI;
    brick:isPartOf ?Water_Loop_URI;
    brick:isPartOf ?Electrical_System_URI.

?Water_Loop_URI a brick:Water_Loop.

?Electrical_System_URI a brick:Electrical_System.

}

ITERATOR ite:CSV(<Equipment.csv>, true, '"', ',','\n',

"NEST Equipment",
"Brick Class",
"Location",
"Feeds",
"meters",
"isSubmeterOf",
"isPartOf_equipment",
"isPartOf_Water_Loop",
"isPartOf_Electrical_System"

)

	AS

?NEST_Equipment
?Brick_Class
?Location
?Feeds
?metered_unit
?isSubmeterOf
?isPartOf_equipment
?isPartOf_Water_Loop
?isPartOf_Electrical_System

WHERE {
    BIND(IRI(CONCAT("https://www.empa.ch/digital-twin/equipment/",?NEST_Equipment)) AS ?NEST_Equipment_URI)
    BIND(IRI(CONCAT("https://brickschema.org/schema/Brick#",ENCODE_FOR_URI(?Brick_Class))) AS ?BrickClass_Type)
    BIND(IRI(CONCAT("https://www.empa.ch/digital-twin/location/",ENCODE_FOR_URI(?Location))) AS ?Location_URI)
    BIND(IRI(CONCAT("https://www.empa.ch/digital-twin/equipment/",ENCODE_FOR_URI(?Feeds))) AS ?feeds_Equipment_URI)
    BIND(IRI(CONCAT("https://www.empa.ch/digital-twin/location/",ENCODE_FOR_URI(?metered_unit))) AS ?metered_unit_URI)
    BIND(IRI(CONCAT("https://www.empa.ch/digital-twin/equipment/",ENCODE_FOR_URI(?isSubmeterOf))) AS ?isSubmeterOf_Equipment_URI)
    BIND(IRI(CONCAT("https://www.empa.ch/digital-twin/equipment/",ENCODE_FOR_URI(?isPartOf_equipment))) AS ?isPartOf_Equipment_URI)
    BIND(IRI(CONCAT("https://www.empa.ch/digital-twin/loops/",ENCODE_FOR_URI(?isPartOf_Water_Loop))) AS ?Water_Loop_URI)
    BIND(IRI(CONCAT("https://www.empa.ch/digital-twin/systems/",ENCODE_FOR_URI(?isPartOf_Electrical_System))) AS ?Electrical_System_URI)
}

